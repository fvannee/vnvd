\section{ANTLR code generator specificatie}
Dit is de specificatie van de code generator in ANTLR.

% VnvdGenerator.g, r182, floris, 2009-07-11 11:11:32 +0200
\begin{lstlisting}
tree grammar VnvdGenerator;

options
{
	tokenVocab = Vnvd;
	language = CSharp2;
	ASTLabelType = TreeNode;
}

@header
{
	using System.Collections.Generic;
	using Vnvd;
}

@namespace
{ 
	Vnvd.Antlr
}

@members
{
	private GeneratorHelper h = new GeneratorHelper();
}

program
	:	^(node=PROGRAM import_stat* { h.BeginProgram(node); } namespace_decl*)
		{
			h.Program(node);
		}
	;

import_stat
	:	^(node=USING qual=qualifier)
		{
			h.Import(node, qual);
		}
	;

namespace_decl
	:	^(node=NAMESPACE qual=qualifier { h.BeginNamespace(node, qual); } (class_decl | interface_decl | enum_decl)*)
		{
			h.Namespace(node, qual);
		}
	;

	
class_decl
	:	^(node=CLASS ^(mod=MODIFIERS modifier*) qual=qualifier (^(ext=EXTENDS qualifier))? (^(imp=IMPLEMENTS qualifier*))?
		{ h.BeginClass(node, h.Modifiers(mod), qual, h.Extendation(ext), h.Implements(imp)); } class_element*)
		{
			h.Class(node, h.Modifiers(mod), qual, h.Extendation(ext), h.Implements(imp));
		}
	;

enum_decl
	:	^(node=ENUM ^(mod=MODIFIERS modifier*) qual=qualifier (^(ENUMOPTION IDENTIFIER))*)
	;
	
interface_decl
	:	^(node=INTERFACE ^(mod=MODIFIERS modifier*) qual=qualifier (^(ext=EXTENDS qualifier))?
		{ h.BeginInterface(node, h.Modifiers(mod), qual, h.Extendation(ext)); } interface_method*)
		{
			h.Interface(node, h.Modifiers(mod), qual, h.Extendation(ext));
		}
	;

interface_method
	:	^(node=IMETHOD qual=type_qualifier id=IDENTIFIER ^(par=PARAMETER (qualifier IDENTIFIER)*))
		{
			h.InterfaceMethod(node, qual, id.Text, h.Parameters(par));
		}
	;

class_element
	:	method_decl
	|	constructor_decl
	|	field_decl
	|	static_constructor
	;
	
literal_value
	:	node=NUMBER
		{
			h.LiteralNumber(node);
		}
	|	node=FLOAT
		{
			h.LiteralFloat(node);
		}
	|	node=STRING
		{
			h.LiteralString(node);
		}
	|	node=CHAR
		{
			h.LiteralCharacter(node);
		}
	|	node=TRUE
		{
			h.LiteralBoolean(node);
		}
	|	node=FALSE
		{
			h.LiteralBoolean(node);
		}
	;
	
constant_literal_value
	:	NUMBER
	|	STRING
	|	FLOAT
	|	CHAR
	|	TRUE
	|	FALSE
	;
	
abstract_mod_list
	:	^(MODIFIERS access_modifier ABSTRACT)
	;

access_modifier
	:	PRIVATE | PUBLIC | PROTECTED | INTERNAL
	;
	
field_decl
	:	^(node=FIELD ^(mod=MODIFIERS modifier*) qual=qualifier id=IDENTIFIER)
		{
			h.Field(node, h.Modifiers(mod), qual, id.Text);
		}
	;
	
method_decl
	:	^(node=METHOD ^(mod=MODIFIERS modifier*) return_=type_qualifier id=IDENTIFIER ^(par=PARAMETER (qualifier IDENTIFIER)*) { h.BeginMethod(node, h.Modifiers(mod), return_, id.Text, h.Parameters(par));} block_expression?)
		{
			h.Method(node, h.Modifiers(mod), return_, id.Text, h.Parameters(par));
		}
	;
	
constructor_decl
	:	^(node=CONSTRUCTOR ^(mod=MODIFIERS modifier*) id=IDENTIFIER ^(par=PARAMETER (qualifier IDENTIFIER)*) { h.BeginConstructorBeforeBaseCall(node); } (^(bas=BASE ^(ALIST argument*)))? { h.BeginConstructor(node, h.Modifiers(mod), h.Parameters(par), bas); } block_expression)
		{
			h.Constructor(node, h.Modifiers(mod), h.Parameters(par));
		}
	;

static_constructor
	:	^(node=SCONSTRUCTOR { h.BeginStaticConstructor(node); } block_expression)
	{
		h.EndStaticConstructor(node); 
	}
	;
	
statement
	:	expression
	|	declaration_statement
	|	const_decl_statement
	|	control_statement
	;
	
control_statement
	:	while_statement
	|	for_statement
	|	try_statement
	|	throw_statement
	;

throw_statement
	:	^(node=THROW expression)
		{
			h.ThrowStatement(node);
		}
	;

try_statement
	:	^(node=TRY { h.BeginTry(node); } block_expression catch_block* finally_block?)
		{
			h.EndTry(node);
		}
	;
	
catch_block
	@init { h.OpenScope(); }
	:	^(node=ICATCH ^(node2=LOCAL qual=qualifier id=IDENTIFIER) { h.DeclarationStatement(node2, qual, id.Text); h.BeginCatch(node, node2, qual, id.Text); } block_expression)
		{
			h.EndCatch(node, qual, id.Text);
			h.CloseScope();		
		}
	;

finally_block
	:	^(node=FINALLY { h.BeginFinally(node); } block_expression)
		{
			h.EndFinally(node);
		}
	;

while_statement
	@init { h.OpenScope(); }
	:	^(node=WHILE { h.WhileStart(node); } scope_expr { h.WhileAfterExpr(node); } block_expression)
		{
			h.WhileLoop(node);
			h.CloseScope();
		}
	;
	
for_statement
	@init { h.OpenScope(); }
	:	^(node=FOR ^(node2=LOCAL qual=qualifier id=IDENTIFIER) expression { h.DeclarationStatement(node2, qual, id.Text); h.BeginFor(node, node2, qual, id.Text); } block_expression)
		{
			h.ForLoop(node);
			
			h.CloseScope();
		}
	;

scope_expr
	:	^(node=SCOPEEXPR statement+)
	{
		h.ScopeExpr(node);
	}
	;
	
declaration_statement
	:	^(node=LOCAL qual=qualifier id=IDENTIFIER)
		{
			h.DeclarationStatement(node, qual, id.Text);
		}
	;

const_decl_statement
	:	^(node=CONST qual=qualifier id=IDENTIFIER const_value)
		{
			h.ConstantDeclaration(node, qual, id.Text);
		}
	;
	
const_value
	:	constant_literal_value
	|	^(node=ARRELEMS (const_array_element)+)
	;
	
const_array_element
	:	^(node=ARRELEM constant_literal_value)
	;
	
expression
	:	assignment_expression
	;

assignment_expression
	:	ternary_expression
	|	^(node=BECOMES assignment_expression assignment_expression)
		{
			h.AssignmentExpr(node);
		}
	|	^(node=ADDEVENT assignment_expression assignment_expression)
		{
			h.AddEventExpr(node);
		}
	|	^(node=REMEVENT assignment_expression assignment_expression)
		{
			h.RemoveEventExpr(node);
		}
	;

ternary_expression
	:	{ h.OpenScope(); }
		^(node=IF scope_expr { h.TernaryThen(node); } expression { h.TernaryElse(node); } expression?)
		{
			h.TernaryExpr(node);
			h.CloseScope();
		}
	|	conditional_or_operator_expression
	;

conditional_or_operator_expression
	:	conditional_and_operator_expression
	|	^(node=OR { h.BeginCondOr(node); } conditional_or_operator_expression { h.MiddleCondOr(node); } conditional_or_operator_expression)
		{
			h.EndConditionalOrExpr(node);
		}
	;

conditional_and_operator_expression
	:	logic_or_operator_expression
	|	^(node=AND { h.BeginCondAnd(node); } conditional_and_operator_expression { h.MiddleCondAnd(node); } conditional_and_operator_expression)
		{
			h.EndConditionalAndExpr(node);
		}
	;

logic_or_operator_expression
	:	logic_xor_operator_expression
	|	^(node=LOR logic_or_operator_expression logic_or_operator_expression)
		{
			h.LogicOrExpr(node);
		}
	;
	
logic_xor_operator_expression
	:	logic_and_operator_expression
	|	^(node=LXOR logic_xor_operator_expression logic_xor_operator_expression)
		{
			h.LogicXorExpr(node);
		}
	;
	
logic_and_operator_expression
	:	logic_expression
	|	^(node=LAND logic_and_operator_expression logic_and_operator_expression)
		{
			h.LogicAndExpr(node);
		}
	;
	
logic_expression
	:	cast_as_expression
	|	^(node=LE logic_expression logic_expression)
		{
			h.LessExpr(node);
		}
	|	^(node=LEQ logic_expression logic_expression)
		{
			h.LessEqualExpr(node);
		}
	|	^(node=GE logic_expression logic_expression)
		{
			h.GreaterExpr(node);
		}
	|	^(node=GEQ logic_expression logic_expression)
		{
			h.GreaterEqualExpr(node);
		}
	|	^(node=EQ logic_expression logic_expression)
		{
			h.EqualExpr(node);
		}
	|	^(node=NEQ logic_expression logic_expression)
		{
			h.NotEqualExpr(node);
		}
	;

cast_as_expression
	:	^(node=AS cast_as_expression qual=qualifier)
		{
			h.CastAsExpression(node, $qual.ids);
		}
	|	^(node=IS cast_as_expression qual=qualifier)
		{
			h.IsTypeExpression(node, $qual.ids);
		}
	|	primary_expression
	;
	
primary_expression
	:	
		(^(PLUS primary_expression primary_expression)) => ^(node=PLUS primary_expression primary_expression)
		{
			h.PlusExpr(node);
		}
	|	(^(MINUS primary_expression primary_expression)) => ^(node=MINUS primary_expression primary_expression)
		{
			h.MinusExpr(node);
		}
	|	secundary_expression
	;
	
secundary_expression
	:	logicnotoperator_expression
	|	^(node=MULTIPLY secundary_expression secundary_expression)
		{
			h.MultiplyExpr(node);
		}
	|	^(node=DIVISION secundary_expression secundary_expression)
		{
			h.DivisionExpr(node);
		}
	|	^(node=MOD secundary_expression secundary_expression)
		{
			h.ModExpr(node);
		}
	;

logicnotoperator_expression
	:	^(node=NOT logicnotoperator_expression)
		{
			h.LogicNotExpr(node);
		}
	|	^(node=PLUS logicnotoperator_expression)
		{
			h.UnaryPlusExpr(node);
		}
	|	^(node=MINUS logicnotoperator_expression)
		{
			h.UnaryMinusExpr(node);
		}
	|	cast_expression
	;

cast_expression
	:	^(node=CAST qual=qualifier cast_expression)
	{
		h.CastExpression(node, qual);
	}
	|	invocation_or_load_expression
	;

invocation_or_load_expression
	:	^(node=DCOLON invocation_or_load_expression id=IDENTIFIER (^(ALIST argument*))?)
		{
			h.InvocationExpr(node, id.Text);
		}
	|	^(node=LBRACKET invocation_or_load_expression expression)
		{
			h.AccessArray(node);
		}
	|	operand
	;
	
operand
	:	^(node=USAGE qual=qualifier)
		{
			h.VariableUsed(node, qual);
		}
	|	literal_value
	|	node=THIS
		{
			h.LiteralThis(node);
		}
	|	node=BASE
		{
			h.LiteralBase(node);
		}
	|	node=NULL
		{
			h.LiteralNull(node);
		}
	|	read_write_expression
	|	object_creation_expression
	|	array_creation_expression
	|	^(node=EXPRBLOCK expression)
		{
			h.ExpressionBlock(node);
		}
	|	block_expression
	|	scope_expr
	;

read_write_expression
	:	^(node=READ qual=IDENTIFIER)
		{
			h.ReadExpression(node, qual.Text);
		}
	|	^(node=READVOID qual=IDENTIFIER)
		{
			h.ReadVoidExpression(node, qual.Text);
		}
	|	^(node=WRITE expression)
		{
			h.WriteExpression(node);
		}
	|	^(node=WRITEVOID expression)
		{
			h.WriteVoidExpression(node);
		}
	;

object_creation_expression
	:	^(node=NEW qual=qualifier (^(ALIST argument*))?)
		{
			h.ObjectCreation(node, qual);
		}
	;
	
array_creation_expression
	:	^(node=NEWARR qualifier expression)
		{
			h.ArrayCreationExpr(node);
		}
	|	^(node=NEWARRELEMS qualifier { h.BeforeArrayElementedCreationExpr(node); } ^(ARRELEMS array_initial_elements*))
		{
			h.ArrayElementedCreationExpr(node);
		}
	;
	
array_initial_elements
	:	^(node=ARRELEM { h.BeforeArrayElementedElement(node); } expression)
		{
			h.ArrayElementedElement(node);
		}
	;
		
block_expression
	:	{ h.OpenScope(); } ^(node=SLIST statement*)
		{
			h.StatementBlock(node);
			h.CloseScope();
		}
	;

argument
	:	^(ARGUMENT expression)
	;

qualifier returns [Qualifier ids]
	:	^(fqual=FQUALIFIER IDENTIFIER+ ARRAY?)
		{
			ids = h.FullQualifier(fqual);
		}
	;
	
type_qualifier returns [Qualifier ids]
	:	v=VOID
		{
			ids = new Qualifier() { "System", "Void" };
		}
	|	qual=qualifier
		{
			ids = h.TypeQualifier(qual);
		}
	;

modifier
	:	PRIVATE | PUBLIC | PROTECTED | INTERNAL | ABSTRACT | STATIC | VIRTUAL | OVERRIDE | INITONLY
	;
\end{lstlisting}
